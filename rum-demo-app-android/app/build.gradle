plugins {
    id 'com.android.application'
}
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.splunk.rum.demoApp"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        all {
            resValue("string", "rum_realm", localProperties['rum.realm'] ?: "")
            resValue("string", "rum_access_token", localProperties['rum.access.token'] ?: "")
            resValue("string", "rum_environment", localProperties['rum.environment'] ?: "")

            buildConfigField("String", "COUNTRY_NAME_FRANCE", "\"France\"")
            buildConfigField("String", "API_CART_END_POINT", "\"cart\"")
            buildConfigField("String", "API_CHECK_OUT_END_POINT", "\"cart/checkout\"")
            buildConfigField("String", "HTTP_404_URL", "\"cart/checkouts\"")
            buildConfigField("String", "HTTP_GENERATE_NEW_SALES_TAX", "\"checkout/calculateTax\"")
            buildConfigField("String", "HTTP_500_URL", "\"payment/send\"")
            buildConfigField("String", "SLOW_API_RESPONSE_URL", "\"cart/clear\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //noinspection SpellCheckingInspection
    compileOptions {
        // Flag to enable support for the new language APIs
        //coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    dataBinding {
        enabled = true
    }
}

configurations {
    all {
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
}

//noinspection SpellCheckingInspection
dependencies {

    //coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    //App Compat And Design Dependency
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.cardview:cardview:1.0.0"
    //don't update to 1.4.1
    // androidx.emoji.text.Spannable Builder.replace method throw android runtime error

    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.5.0'

    // Glide Image Loading Library
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    //Web View
    implementation 'androidx.webkit:webkit:1.4.0'

    //Retrofit Networking Dependency
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'

    //Retrofit Http Logging Dependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.google.code.gson:gson:2.8.8'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // Dagger 2 Dependency Injection
    implementation 'com.google.dagger:dagger:2.38.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'


    annotationProcessor 'com.google.dagger:dagger-compiler:2.38.1'

    // Android Architecture Component ViewModel,LiveData
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.4.1'

    // Navigation Fragment & Navigation View
    implementation 'androidx.navigation:navigation-fragment:2.4.1'
    implementation 'androidx.navigation:navigation-ui:2.4.1'

    // Location Permission
    implementation 'com.google.android.gms:play-services-location:19.0.1'

    // Parcel Library
    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'

    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'com.google.maps.android:android-maps-utils:2.3.0'



    // Splunk RUM Dependency
    implementation 'com.splunk:splunk-otel-android:0.12.0'

}